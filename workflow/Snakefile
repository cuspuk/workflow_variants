from snakemake.utils import min_version

min_version("7.25.0")


include: "rules/common.smk"


module mapping_workflow:
    snakefile:
        github("xsitarcik/mapping", path="workflow/Snakefile", tag=config["module_versions"]["mapping"])
    config:
        config


use rule * from mapping_workflow as mapping_*


for method in config["variants"]["callers"]:

    include: f"rules/variants/{method}.smk"


for method in config["consensus"]["callers"]:

    include: f"rules/consensus/{method}.smk"


# Only include standalone rules if the module is used standalone, i.e. not as a part of a larger workflow
if not config.get("module_versions") or "variants" not in config["module_versions"]:

    include: "rules/standalone.smk"


include: "rules/stats.smk"
include: "rules/summary.smk"


rule all:
    input:
        **mapping_workflow.get_outputs(),
        **get_outputs(),
        **get_standalone_outputs(),
    default_target: True
    localrule: True


onstart:
    if extra := get_all_relevant_extra_params():
        logger.info("Variants module - extra parameters parsed from config:")
        logger.info(get_all_relevant_extra_params())
